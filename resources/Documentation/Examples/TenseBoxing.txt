Example: * Tense Boxing
Location: The past and perfect tenses
RecipeLocation: Memory and Knowledge
Index: Past tense conditions
Description: An overview of all the variations of past and present tenses, and how they might be used.
For: Z-Machine

Here we have a box that prints out its current state and its history each time we open and close it:

	{*}"Tense Boxing"
	
	The Temporal Prism is a room. "A room of angled mirrors, in whose surfaces you can see what is now; what just was; what has always been. A final mirror is broken and its frame gapes blackly."
	
	The mysterious box is in the Temporal Prism. It is an openable closed container.
	
	To assess the box:
		if the box was not open, say "The box was not open.";
		if the box was open, say "The box was open.";
		if the box had not been open, say "The box had not been open.";
		if the box had been open, say "The box had been open.";
		if the box is not open, say "The box is not open.";
		if the box is open, say "The box is open.";
		if the box has not been open, say "The box has not been open.";
		if the box has been open, say "The box has been open."
	
	Before opening the mysterious box:
		say "You are about to open the box.";
		assess the box.
	
	Before closing the mysterious box:
		say "You are about to close the box.";
		assess the box.
	
	After opening the mysterious box:
		say "You now open the box.";
		assess the box.
	
	After closing the mysterious box:
		say "You now close the box.";
		assess the box.

Note that `was` and `was not` and so on may describe conditions more complicated than simple properties: we could equally well ask `if the box has been in the sack`, `if the box had been carried by the player`, and so on.

The past (`if the box was...`) and past perfect (`if the box had been...`) are especially useful for cases where we want to report on an action after the state of the item has changed; so, for instance:

	{**}After taking the mysterious box:
		if the box had not been carried by the player, say "You lift the mysterious box for the first time.";
		if the box had been carried by the player, say "You again pick up the mysterious box."
	
	Test me with "open box / close box / open box / take box / drop box / take box".

This is in many respects similar to a rule beginning `After taking the mysterious box for the first time...`, but it is superior in most circumstances, for two reasons.

First, it will respond correctly even if the player has somehow carried the box before without taking it explicitly: for instance, if another character gave them the box, if the box were moved into their inventory as a result of another action, or if the player carried the box at the start of play. Inform begins its reckoning of time when the game begins, so if the box is defined as being open at the outset, `if the box has been open` will always be true.

Second, `after taking... for the first time` fires only the first time the player attempts to take something. If the player tried to take the box, failed, and then tried again later, the `for the first time...` rule would not fire; our `if the box has not been carried...` rule would.

