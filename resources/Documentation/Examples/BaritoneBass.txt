Example: *** Baritone, Bass
Location: Understanding values
RecipeLocation: Traits Determined By the Player
Index: Allowing the player to describe the main character before starting play
Description: Letting the player pick a vocal range (or perhaps other characteristics) before starting play.
For: Z-Machine

The `reading a command` activity is rather advanced; for the moment, what we need to understand is that we're intervening in commands at the start of play and insisting that the player's first instruction to the game consist of a choice of vocal range. After that point, it will be set and play will proceed as normal.

	{*}"Baritone, Bass"
	
	Getting Started is a room.

	Vocal range is a kind of value. The vocal ranges are soprano, mezzosoprano, contralto, countertenor, tenor, baritone, bass, and unknown.

	A person has a vocal range. The vocal range of the player is unknown.

	When play begins:
		now the command prompt is "Please choose a vocal range for your character. >".

	After reading a command when the vocal range of the player is unknown:
		if the player's command includes "[vocal range]":
			now the vocal range of the player is the vocal range understood;
			if the vocal range of the player is unknown:
				say "This story requires a selection of vocal range. [run paragraph on]";
				reject the player's command;
			say "[line break]Thank you. We now begin...";
			now the command prompt is ">";
			move the player to Sandy Beach;
			reject the player's command;
		otherwise:
			say "Sorry, we're not ready to go on yet. [run paragraph on]";
			reject the player's command.

	Sandy Beach is a room.

	Singing is an action applying to nothing. Understand "sing" as singing.

	Instead of singing:
		say "You sound off with a glorious [vocal range of the player]!"

If we had a whole series of things to ask the player about, we might define a whole series of kinds of value:

	The hairstyles are quiffed, beehive, bangs...

and use a `construction stage` variable to keep track of the current stage of character-construction, as in

	After reading a command when the current construction stage is choosing a hairstyle:
		...
