Example: ** Solitude
Location: New rulebooks
RecipeLocation: Helping and Hinting
Index: Novice mode offers suggestions before each prompt
Description: Novice mode that prefaces every prompt with a list of possible commands the player could try, and highlights every important word used, to alert players to interactive items in the scenery.
For: Z-Machine

Observation of novice IF players suggests that they often have a hard time figuring out how to get started, especially if they are encountering the game in a context where they don't have time to settle in and read instructions. Here we provide some training wheels to help them learn to communicate.

This is divided into several parts. The first part is the system of rules for general guidance, which could be excerpted and used anywhere. The second part is a scenario using these rules.

	{*}"Solitude"
	
	Part 1 - General Rules
	
	When play begins:
		say "Have you played interactive fiction before? >";
		if the player consents, now novice mode is false.

The rationale for asking the question this way, and not another, is that novices asked whether they would like instructions very often say no, even if they need them.

	{**}Novice mode is a truth state that varies. Novice mode is true.
	
	Stopping novice mode is an action out of world.
	Starting novice mode is an action out of world.
	
	Understand "novice mode off" or "novice off" as stopping novice mode.
	Understand "novice mode on" or "novice on" as starting novice mode.
	
	Carry out stopping novice mode: now novice mode is false.
	Carry out starting novice mode: now novice mode is true.
	
	Report stopping novice mode: say "Novice mode is now off."
	Report starting novice mode: say "Novice mode is now on."
	
	Before reading a command when novice mode is true:
		say "[line break]Some options to try:[line break]";
		follow the novice suggestion rules.
	
	The novice suggestion rules is a rulebook.
	
	The previous action is an action that varies.
	Before doing anything, now the previous action is the current action.
	
	A novice suggestion rule (this is the suggestion that they look rule):
		unless the previous action is looking or the previous action is going, say "  [bold type]look[roman type]".
	
	A novice suggestion rule (this is the suggestion that they check inventory rule):
		if the player carries something and we are not taking inventory, say "  [bold type]inventory[roman type] (I)".
	
	A novice suggestion rule (this is the suggestion that they put things on rule):
		if the player carries something and a free-standing supporter is relevant, say "  [bold type]put[roman type] something [bold type]on[roman type] [the list of relevant supporters]".
	
	A novice suggestion rule (this is the suggestion that they take things rule):
		if a gettable thing is relevant, say "  [bold type]take[roman type] [the list of gettable relevant things]".
	
	A novice suggestion rule (this is the suggestion that they examine things rule):
		if an unexamined thing is relevant, say "  [bold type]examine[roman type] (X) [the list of unexamined relevant things]".
	
	A novice suggestion rule (this is the suggestion that they enter things rule):
		if a relevant thing is worth entering, say "  [bold type]enter[roman type] [the list of worth entering relevant things], or [bold type]get out[roman type]".
	
	A novice suggestion rule (this is the suggestion that they open things rule):
		if an unlocked openable thing is relevant, say "  [bold type]open[roman type] or [bold type]close[roman type] [the list of unlocked openable relevant things]".
	
	A novice suggestion rule (this is the suggestion that they lock things rule):
		if a closed lockable thing is relevant, say "  [bold type]lock[roman type] or [bold type]unlock[roman type] [the list of closed lockable relevant things]".
	
	A novice suggestion rule (this is the suggestion that they eat things rule):
		if the player carries an edible relevant thing, say "  [bold type]eat[roman type] [the list of edible relevant things carried by the player]".
	
	A novice suggestion rule (this is the suggestion that they wear things rule):
		if the player carries a wearable relevant thing, say "  [bold type]wear[roman type] [the list of wearable relevant things carried by the player]".
	
	A novice suggestion rule (this is the suggestion that they turn things on rule):
		if a device is relevant, say "  [bold type]turn on[roman type] or [bold type]turn off[roman type] [the list of relevant devices]".
	
	A novice suggestion rule (this is the suggestion that they go places rule):
		if a room is adjacent, say "  [bold type]go[roman type][exit list][if in darkness] or try other directions in the dark[otherwise]".
	
	A novice suggestion rule (this is the suggestion that they enter doors rule):
		if an open door is relevant, say "  [bold type]go through[roman type] [the list of relevant open doors]".
	
	A novice suggestion rule (this is the suggestion that they interact with people rule):
		if another person is relevant, say "  [bold type]kiss[roman type] or [bold type]wake[roman type] [the list of relevant other people][if the player carries something], or [bold type]give[roman type] things [bold type]to[roman type] someone[end if]".
	
	A novice suggestion rule (this is the suggestion that they ask for help rule):
		say "  [bold type]help[roman type] to see a more complete set of instructions".
	
	A novice suggestion rule (this is the suggestion that they turn off help rule):
		say "  [bold type]novice mode off[roman type] to turn off this guidance".
	
	Last novice suggestion rule:
		say "[line break]".

The suggestion about asking for help is no good unless we provide some. This might take any of a number of forms, but for the sake of example we'll use an easy way out:

	{**}Include Basic Screen Effects by Emily Short. Include Menus by Emily Short. Include Basic Help Menu by Emily Short.
	
	After taking inventory when novice mode is true: say "To get rid of any of these objects, [bold type]drop[roman type] it."
	
	A thing can be examined or unexamined. Carry out examining something: now the noun is examined.
	
	A thing can be seen or unseen. A thing is usually unseen.
	
	Definition: a thing is relevant if it is seen and it is visible. Before printing the name of something (called the target): now the target is seen; if novice mode is true, say "[bold type]". After printing the name of something: say "[roman type]".
	
	Definition: a supporter is worth entering:
		if the player carries it, no;
		if it is enterable, yes.
	
	Definition: a container is worth entering:
		if the player carries it, no;
		if it is enterable and it is open, yes.
	
	Definition: a person is other if it is not the player. Definition: a person is another if it is other.
	
	Definition: a thing is free-standing if it is in a room.
	
	To say exit list:
		let place be location;
		let count be 0;
		repeat with way running through directions:
			let place be the room way from the location;
			if place is a room:
				increment count;
				say "[if count is greater than 1] or[end if] [bold type][way][roman type]".
	
	Definition: a thing is gettable:
		if it is scenery, no;
		if it is fixed in place, no;
		if it is a person, no;
		if the player is carrying it, no;
		if the player is wearing it, no;
		yes.
	
	Part 2 - On the Ground
	
	Antarctic Research Station is a room. "Though not always the most stimulating of environments, the station is far from your ex-wife and most of the things in the world that annoy you, namely the other 6+ billion people. There is a second room to the south." The station contains a radio. The radio is a device. It is fixed in place.
	
	South of the Station is Sitting Room. The description of the Sitting Room is "Just big enough for a very [comfortable chair]." The Sitting Room contains an enterable supporter called a comfortable chair. The chair is scenery. A monograph about penguins is in the Sitting Room.
	
	Blistering Cold is a room. "It is white out here and very very very cold." The white door is a door. "[The white door] leads to [the other side of the white door]."  It is west of the Blistering Cold and east of the Antarctic Research Station.
	
	Test me with "i / x radio / x door / s / i / x chair / x monograph / sit in chair / get up / n / open door / enter door".

